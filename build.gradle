plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.jreleaser' version '1.20.0'
}

repositories {
    mavenLocal()
    mavenCentral()
}

group = 'co.digitalforge'
version = '1.1.0'

dependencies {
    testImplementation platform('org.junit:junit-bom:5.13.0')
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
    toolchain {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

jreleaser {
    deploy {
        maven {
            mavenCentral {
                portal {
                    stagingRepository('build/release-repo')
                }
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {

                name = 'SneakyThrow'
                description = 'Sneakily rethrow checked exceptions.'
                url = 'https://github.com/DigitalForgeSoftworks/SneakyThrow/'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'zreed'
                        name = 'Zachary Reed'
                        email = 'zach@digitalforge.co'
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com:DigitalForgeSoftworks/SneakyThrow.git'
                    developerConnection = 'scm:git:git@github.com:DigitalForgeSoftworks/SneakyThrow.git'
                    url = 'https://github.com/DigitalForgeSoftworks/SneakyThrow'
                }

            }
        }
        relocation(MavenPublication) {
            pom {
                groupId = "org.digitalforge"
                distributionManagement {
                    relocation {
                        groupId = "co.digitalforge"
                    }
                }
            }
        }
    }
}
